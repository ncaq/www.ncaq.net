---
title: 2016年02月に単純なLispを実装した
---

* 2016年02月に単純なLispを実装した
* 面白みはない

# 背景

[48時間でSchemeを書こう - Wikibooks](https://ja.wikibooks.org/wiki/48%E6%99%82%E9%96%93%E3%81%A7Scheme%E3%82%92%E6%9B%B8%E3%81%93%E3%81%86)を見たら案外簡単そうだと思って手を動かしてみたようだ.

継続とレキシカルスコープの実装が難しく,やらねばならない課題などがあった(これは課題でも何でもない)ので,これ以上実装は進められなかった.

かと言って教材をそのまま丸コピペするのも面白くなかった.

フィボナッチ数を計算してそれで満足した.

# ソースコード

Parsecを使うのは初めてだった.
それどころか,抽象構文木を作るようなテキスト解析処理を書いた事自体当時初めてだったので,コードの拙さが伝わってくる.
例えば`try`を乱用している.

~~~hs
module SimpLisp where

import           Control.Monad
import           Data.Maybe
import           Text.ParserCombinators.Parsec

data LVal = LAtom String
          | LList [LVal]
          | LNumber Double
          | LString String
          | LBool Bool
          | LFunction [LVal] LVal
          | LIf LVal LVal LVal
          deriving (Eq, Ord, Show, Read)

type LScope = [(LVal, LVal)]

run :: String -> Either ParseError LVal
run s = eval [] <$> readExpr s

eval :: LScope -> LVal -> LVal
eval scope (LAtom atom) = fromJust $ lookup (LAtom atom) scope
eval _     (LList [LAtom "quote", val]) = val
eval scope (LList (LFunction binds body : args)) = eval (zip binds (map (eval scope) args) ++ scope) body
eval scope (LList (LAtom func : args)) = case lookup (LAtom func) scope of
    Just f -> eval scope (LList (f : args))
    _ -> (fromJust $ lookup func primitives) $ map (eval scope) args
eval scope (LIf p t f) = case eval scope p of
    LBool False -> eval scope f
    _ -> eval scope t
eval _ n = n

primitives :: [(String, [LVal] -> LVal)]
primitives = [ ("+", numericBinop (+))
             , ("-", numericBinop (-))
             , ("*", numericBinop (*))
             , ("/", numericBinop (/))
             , ("log", numericBinop logBase)
             , ("mod", numericBinop (\a b -> fromInteger $ mod (floor a) (floor b)))
             , ("show", LString . show . head)
             , ("=", \[a, b] -> LBool $ a == b)
             ]

numericBinop :: (Double -> Double -> Double) -> [LVal] -> LVal
numericBinop op params = LNumber . foldl1 op $ map unpackNum params

unpackNum :: LVal -> Double
unpackNum (LNumber n) = n
unpackNum n = error $ "it is not number: " ++ show n

readExpr :: String -> Either ParseError LVal
readExpr = parse parseExpr "SimpLisp"

symbol :: Parser Char
symbol = oneOf "!#$%&|*+-/:<=>?@^_~"

parseExpr :: Parser LVal
parseExpr = spaces *>
            ( try parseIf <|>
              try parseFunction <|>
              parseList <|>
              parseQuoted <|>
              parseString <|>
              parseNumber <|>
              parseAtom)

parseAtom :: Parser LVal
parseAtom = do
    first <- letter <|> symbol
    rest <- many (letter <|> digit <|> symbol)
    return $ case first : rest of
        "#t" -> LBool True
        "#f" -> LBool False
        atom -> LAtom atom

parseList :: Parser LVal
parseList = char '(' *> liftM LList (sepBy parseExpr spaces) <* char ')'

parseNumber :: Parser LVal
parseNumber = liftM (LNumber . read) $ many1 digit

parseString :: Parser LVal
parseString = do
    _ <- char '"'
    x <- many (noneOf "\"")
    _ <- char '"'
    return $ LString x

-- " workaround highlight.js bug

parseQuoted :: Parser LVal
parseQuoted = do
   _ <- char '\''
   x <- parseExpr
   return $ LList [LAtom "quote", x]

parseFunction :: Parser LVal
parseFunction = do
    _ <- char '('
    _ <- string "lambda"
    _ <- spaces
    args <- maybeAtoms <$> parseList
    _ <- spaces
    f <- parseExpr
    _ <- char ')'
    return $ LFunction (fromJust args) f

parseIf :: Parser LVal
parseIf = do
    _ <- char '('
    _ <- string "if"
    p <- parseExpr
    t <- parseExpr
    f <- parseExpr
    _ <- char ')'
    return $ LIf p t f

maybeAtoms :: LVal -> Maybe [LVal]
maybeAtoms (LList xs) = mapM maybeAtom xs
maybeAtoms _ = Nothing

maybeAtom :: LVal -> Maybe LVal
maybeAtom x@(LAtom _) = Just x
maybeAtom _ = Nothing
~~~

# 感想

もうちょっとオリジナリティのある言語を作ったほうが教材と被らなくて面白みがあって良さそうだ.

まだ単純な言語とネタ言語しか作ったことがないので,今度はもうちょっと応用的なものを書いてみたい.

しかしParsecやHaskellの学習にはなった.
次のネタ言語ではApplicativeやStateモナドを使っている.
